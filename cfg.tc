%option lang = "C++"
%option state_type = "CFGNodeType"
%option reentrant

%decls %{
#include <iostream>
#include <string>
#include <sstream>
#include <unordered_set>
#include <unordered_map>
#include <list>
#include <queue>
#include "ast.h"

class CFGNode;
class CFGExpr;
class CFGStmt;

using std::string;
using std::cout;

using CFGExprList = std::list<CFGExpr *>;
using CFGStmtList = std::list<CFGStmt *>;
using CFGStmtPair = std::pair<CFGStmt *, CFGStmt *>;
using CFGStmtVector = std::vector<CFGStmt *>;

void CFGHVisit(CFGStmt *root);
void CFGSetLabels(CFGStmt *root);
CFGStmt *CFGMakeBasicBlocks(CFGStmt *root);
std::string CFGToString(CFGStmt *root);
CFGStmt *CFGRemoveNops(CFGStmt *root);

%}

%{
    #include "cfg.h"
%}

%node CFGNode %abstract %typedef = {
}

%node CFGStmt CFGNode %abstract = {
    %nocreate string label = {""};
    %nocreate int pred_count = {0};
}

%node CFGSimpleStmt CFGStmt %abstract = {
    %nocreate CFGStmt *next = {nullptr};
}

%node CFGBlockStmt CFGStmt = {
    CFGStmt *first;
    CFGStmt *last;
}

%node CFGNop CFGSimpleStmt

%node CFGCBRStmt CFGStmt = {
    CFGExpr *cond;
    CFGStmt *target_true;
    CFGStmt *target_false;
}

%node CFGAssign CFGSimpleStmt = {
    CFGOperand *lhs;
    CFGExpr *rhs;
}

%node CFGRetStmt CFGSimpleStmt = {
    CFGExpr *expr;
}

%node CFGParamStmt CFGSimpleStmt = {
    CFGOperand *operand;
}

%node CFGCallStmt CFGSimpleStmt = {
    string name;
    int arg_count;
}

%node CFGExpr CFGNode %abstract

%node CFGOperand CFGExpr %abstract

%node CFGNumOper CFGOperand = {
    int value;
}

%node CFGStrOperand CFGOperand = {
    string value;
}

%node CFGLhs CFGOperand = {
    string id;
}

%node CFGBinaryExpr CFGExpr %abstract = {
    CFGOperand *operand1;
    CFGOperand *operand2;
}

%node CFGAddExpr CFGBinaryExpr
%node CFGSubExpr CFGBinaryExpr
%node CFGMulExpr CFGBinaryExpr
%node CFGDivExpr CFGBinaryExpr
%node CFGModExpr CFGBinaryExpr
%node CFGGTExpr CFGBinaryExpr
%node CFGGEExpr CFGBinaryExpr
%node CFGLTExpr CFGBinaryExpr
%node CFGLEExpr CFGBinaryExpr
%node CFGEQExpr CFGBinaryExpr
%node CFGNEExpr CFGBinaryExpr

%node CFGUnaryExpr CFGExpr = {
    CFGExpr *expr;
}

%node CFGNegExpr CFGExpr = {
    CFGOperand *operand;
}

%operation %virtual string toString(CFGNode *)
%operation %virtual void setNext(CFGStmt* this, CFGStmt *nstmt)
%operation %virtual void addPred(CFGStmt *, CFGStmt *stmt)
%operation %virtual CFGStmt *getNext(CFGStmt *)
%operation %virtual int edgeCount(CFGStmt *)
%operation %virtual CFGStmtVector getEdges(CFGStmt *)

edgeCount(CFGStmt) { return 0; }
edgeCount(CFGSimpleStmt) { return (next != nullptr)? 1 : 0; }
edgeCount(CFGCBRStmt) { return 2; }

setNext(CFGStmt) { /* Nothing */ }

setNext(CFGSimpleStmt) {
    next = nstmt;
    nstmt->addPred(this);
}

addPred(CFGStmt) { pred_count++; }

getNext(CFGStmt) { return nullptr; }
getNext(CFGSimpleStmt) { return next; }
getNext(CFGBlockStmt) { return last->getNext(); }

getEdges(CFGSimpleStmt) { 
    CFGStmtVector edges;

    if (next != nullptr) {
        edges.push_back(next);
    }

    return edges;
}

getEdges(CFGCBRStmt) {
    CFGStmtVector edges(2);

    edges[0] = target_true;
    edges[1] = target_false;
    return edges;
}

getEdges(CFGBlockStmt) {
    return last->getEdges();
}

toString(CFGNode) { return  ""; }

toString(CFGStrOperand) {
    return "\"" + value + "\"";
}

toString(CFGNumOper) {
    return std::to_string(value);
}

toString(CFGLhs) {
    return id;
}

toString(CFGNegExpr) {
    return "-" + operand->toString();
}

toString(CFGCallStmt) {
    std::ostringstream out;

    out << "call " << name << ", " << arg_count;

    return out.str();    
}

toString(CFGParamStmt) {
    return "param " + operand->toString();
}

toString(CFGNop) {
    return "nop";
}

toString(CFGCBRStmt) {
    std::ostringstream out;

    out << "cbr(" << cond->toString() << ", "
        << target_true->label << ", "
        << target_false->label << ")";

    return out.str();
}

toString(CFGAssign) {
    std::ostringstream out;

    out << lhs->toString() << " = " << rhs->toString();

    return out.str();
}

toString(CFGBlockStmt) {
    std::ostringstream out;

    CFGStmt *s = first;
    while (s != last ) {
        out << s->toString() << '\n';
        s = s->getNext();
    }
    out << last->toString() << '\n';

    return out.str();
}

toString(CFGRetStmt) {
    return "ret";
}

toString(CFGUnaryExpr) {
    return expr->toString();
}

%operation %virtual string genCode(CFGStmt* this, Context& ctx)

genCode(CFGStmt) {
    cerr << "not implemented yet\n";
    throw "error";
}

genCode(CFGParamStmt) {
    string s;
    s = "\tpush dword " + ctx.getLabelfromStrTable(operand->toString());
    s += '\n';
    return s;
}

genCode(CFGCallStmt) {
    string s;
    s = "\tcall " + name + '\n';
    s += "\tadd esp, " + to_string(arg_count*4);
    s += '\n';
    return s;
}